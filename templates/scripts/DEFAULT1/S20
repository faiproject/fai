#! /usr/bin/cfengine

control:
   OutputPrefix = ("cfengine")
   actionsequence = ( files directories links editfiles )
   EditFileSize = ( 30000 )

files:
   any::
   # suppress emacs errors
        ${target}/etc/mailname mode=644 owner=0 group=0 action=touch

# floppy and cdroms are accessible for all
        ${target}/dev include=fd* mode=666   action=fixall r=1
        ${target}/dev include=sr* mode=444   action=fixall r=1

directories:
   any::
	${target}/tmp mode=1777 owner=0 group=0

   BSDAMD::
        ${target}/scratch/. mode=755 owner=0 group=0

   FILES_SCRATCH::
        ${target}/files/scratch/. mode=1777 owner=0 group=0

   SCRATCH::
        ${target}/scratch mode=1777 owner=0 group=0

links:
   LINUX.CS_KOELN::
	${target}/vol -> /usr/local/vol nofile=force

editfiles:
   any::
	{ ${target}/etc/default/rcS
	  ReplaceAll "^UTC=.*" With "UTC=${UTC}"
	}

	{ ${target}/etc/init.d/sysklogd
	  ReplaceAll "^SYSLOGD=.*" With "SYSLOGD=${dblquote}-m 360${dblquote}"
	}

	{ ${target}/etc/fstab
	  AppendIfNoSuchLine "none  /proc/bus/usb  usbdevfs  defaults"
	  AppendIfNoSuchLine "/dev/fd0  /floppy  auto  users,noauto 0 0"
	}

	{ ${target}/etc/locale.gen
	  AutoCreate
	  AppendIfNoSuchLine "en_US ISO-8859-1"
	}

	# insert some important hosts
	{ ${target}/etc/hosts
	  AppendIfNoSuchLine "127.0.0.1       localhost"
	}

# add second root account using tcsh
# insert encrypted root password
	{ ${target}/etc/passwd
	  LocateLineMatching 	"^root:.*"
	  InsertLine 		"roott::0:0:root:/root:/usr/bin/tcsh"
	  ReplaceAll 		"^root::" With "root:${rootpw}:"
	  ReplaceAll		"^roott::" With "roott:${rootpw}:"
	}

	{ ${target}/etc/syslog.conf
	  ReplaceAll "daemon.*/var/log/daemon.log"
		With "daemon.warn${tab}${tab}${tab}-/var/log/daemon.log"
	  ReplaceAll "kern.*/var/log/kern.log"
		With "kern.warn${tab}${tab}${tab}-/var/log/kern.log"
	  AppendIfNoSuchLine "local6.debug${tab}${tab}${tab}/var/log/daemon.log"
	}


   GERMAN::
	{ ${target}/etc/locale.gen
	  AutoCreate
	  AppendIfNoSuchLine "de_DE ISO-8859-1"
	  AppendIfNoSuchLine "de_DE@euro ISO-8859-15"
	}

   CS_KOELN::
	# a2ps write to stdout
	{ ${target}/etc/a2ps-site.cfg
	  AppendIfNoSuchLine "Options: --borders=no --output=-"
	}

	# insert some important hosts
	{ ${target}/etc/hosts
	  AppendIfNoSuchLine "134.95.9.10     rubens"
	}

	{ ${target}/etc/ssh/sshd_config
	  ReplaceAll "X11Forwarding no"
	        With "X11Forwarding yes"
	}

   HOME_CLIENT::
	{ ${target}/etc/fstab
	  HashCommentLinesContaining "/home "
	  AppendIfNoSuchLine "${hserver}:/home /home nfs rw,nosuid 0 0"
	}

   DATALESS::
	{ ${target}/etc/fstab
	  HashCommentLinesContaining "/usr "
	  AppendIfNoSuchLine "${bserver}:/usr /usr nfs ro 0 0"
	}

   USR_LOCAL_MOUNT::
	{ ${target}/etc/fstab
	  HashCommentLinesContaining "/usr/local "
	  AppendIfNoSuchLine "${bserver}:/usr/local /usr/local nfs ro 0 0"
	}

   SCRATCH::
	{ ${target}/etc/exports
	  AutoCreate
	  AppendIfNoSuchLine "/scratch${tab}${tab} @faiclients(rw,no_root_squash)"
	}

   FILES_SCRATCH.!NIS::
	{ ${target}/etc/exports
	  AutoCreate
	  AppendIfNoSuchLine "/files/scratch${tab}${tab} @faiclients(rw,no_root_squash)"
	}

# if system is NET_9 and NIS member enable access for sundomain too.
   FILES_SCRATCH.NET_9.NIS::
        { ${target}/etc/exports
          HashCommentLinesContaining "/files/scratch"
          AppendIfNoSuchLine "/files/scratch${tab}${tab} @faiclients(rw,no_root_squash) @sundomain(rw,no_root_squash)"
        }

   HOME_SERVER::
	{ ${target}/etc/exports
	  AppendIfNoSuchLine "/usr${tab}${tab}@faiclients(ro,no_root_squash)"
	  AppendIfNoSuchLine "/home${tab}${tab}@sundomain(rw,no_root_squash) @faiclients(rw,no_root_squash)"
	}

   SERVER::
	{ ${target}/etc/shells
	  AppendIfNoSuchLine "/usr/local/bin/tcsh"
	}	  

   NOGETTY::
	{ ${target}/etc/inittab
	  HashCommentLinesContaining "respawn:/sbin/getty "
	}

# add an account so everybody can shutdown the notebook
# this account has no password!
   NOTEBOOK::
	{ ${target}/etc/passwd
	  AppendIfNoSuchLine "poweroff::0:0:poweroff account:/tmp:/sbin/poweroff"
	}

# Install users for netscape kiosk-mode
   WWWKIOSK::
	{ ${target}/etc/passwd
	  AppendIfNoSuchLine "halt:${haltpw}:0:0:,,,:/root/:/root/.shutdown.sh"
	  AppendIfNoSuchLine "opac:${opacpw}:20000:20000:OPAC WI/INF,,,:/home/opac:/bin/bash"
	}
	
	{ ${target}/etc/group
	  AppendIfNoSuchLine "opac:*:20000:"
	}

	#leave only one tty for login. First tty has the entry
        #..:2345:... in inittab
	{ ${target}/etc/inittab
	  HashCommentLinesContaining ":23:respawn:/sbin/getty"
        }

# mount shm filesystem
   kueppers|dom::
        { ${target}/etc/fstab
	  AppendIfNoSuchLine "none  /dev/shm  shm  defaults  0 0"
        }

# add NIS printcap
   NISLPRCLIENT::
        { ${target}/etc/lprng/lpd.conf
	  AppendIfNoSuchLine "printcap_path=|/usr/local/share/LPRng/pcfilter"
	}

# add scratch.map 
   BSDAMD::
        { ${target}/etc/am-utils/amd.conf
	  AppendIfNoSuchLine "[ /scratch ]"
	  AppendIfNoSuchLine "map_name = amd.scratch"
	}

	{ ${target}/etc/default/am-utils
	  ReplaceAll "AM_UTILS_MAP_NET='true'"
	        With "AM_UTILS_MAP_NET='false'"
	}

# add netgroup to passwd if client is in NIS and NET_9 and LINUX
   NIS.LINUX.NET_9::
	{ ${target}/etc/passwd
	  AppendIfNoSuchLine "+@linuxusers::0:0:::"
	}
