#! /usr/bin/perl

# $Id$
#*********************************************************************
#
# fcopy -- copy files using FAI classes and preserve directory structure
#
# This script is part of FAI (Fully Automatic Installation)
# Copyright (C) 2000-2002 Thomas Lange, lange@informatik.uni-koeln.de
# Universitaet zu Koeln
#
#*********************************************************************
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 2 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful, but
# WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
# General Public License for more details.
# 
# A copy of the GNU General Public License is available as
# '/usr/share/common-licences/GPL' in the Debian GNU/Linux distribution
# or on the World Wide Web at http://www.gnu.org/copyleft/gpl.html.  You
# can also obtain it by writing to the Free Software Foundation, Inc.,
# 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.
#*********************************************************************

my $version = "Version 1.9.4, 4-may-2002";

use strict;
use File::Copy;
use File::Compare;
use File::Find;
use File::Path;
use File::Basename;
use Getopt::Std;

use vars qw/*name/;

# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
# Global variables
my $debug;
my $error;
my $verbose;
my $target;
my $source;
my @classes;

my @opt_modes;
my @rlist;
my %changed;
my %lastclass;
my $modeset;

# getopts:
our ($opt_s, $opt_t, $opt_r, $opt_m, $opt_v, $opt_d, $opt_D);
our ($opt_c, $opt_C, $opt_h, $opt_F, $opt_l, $opt_L, $opt_P);

# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
sub copy_one {

  # copy file $prefix/$source/$class to $target/$source
  my ($prefix,$source,$target) = @_;
  my ($class,$sourcefile,$destfile);
  my $ps = "$prefix/$source";
  my $tpath = "$target/" . dirname $source;
  my $preserve = 0;
  my $logcomment = "";

  warn "copy_one: source: $source: ps: $ps tpath: $tpath\n" if $debug;

  # $prefix/$source must be a directory
  unless (-d $ps) { ewarn("$ps is not a directory. Not copied.");return }
  # use the last class for which a file exists
  foreach (@classes) { $class = $_,last if -f "$ps/$_"; }
  $destfile = "$target/$source";

  unless (defined $class) {
    ewarn("no matching file for any class for $source defined.");
    # do not copy
    $opt_d and -f $destfile and move($destfile,"$destfile.pre_fcopy");
    return;
  }
  warn "using class: $class\n" if $debug;
  $sourcefile = "$ps/$class";

  # do nothing if source and destination files are equal 
  if ($opt_P) {
    # compare logically
    if ($lastclass{$source}) {
      # $source has already been copied last time

      if ($lastclass{$source} ne $class) {
        $logcomment = "\t# changed class" if $opt_L;
      } else {
        if ($changed{"$source/$class"} or 
            $changed{"$source/postinst"} or 
            $changed{"$source/file-modes"}) {
          $logcomment = "\t# changed file" if $opt_L;
        } else {
          $logcomment = "\t# preserved (logical)" if $opt_L;
          $preserve = 1;
        }
      }
    } else {
      $logcomment = "\t# new (logical)" if $opt_L;
    }
  } else {
    # compare literally

    if ( compare($sourcefile,$destfile)) {
      $logcomment="\t# new (literal)";
    } else {
      $logcomment="\t# preserved (literal)" if $opt_L;
      $preserve = 1;
    }
  }
  #if a package is being purged, our information about its config files is
  #wrong, so first check if they exist. if not, don't preserve, but copy
  if ($preserve && ! -e $destfile) {
    $logcomment="\t# magically disappeared (maybe purged)";
    $preserve=0;
  }
  
  print LOGFILE "$source\t$class$logcomment\n" if $opt_L;
  if ($preserve) { 
    ewarn("preserving $source \n");
    return;
  }

  # if destination is a symlink and -l is given, complain about it
  if ($opt_l && -l $destfile) {
    ewarn("Destination $destfile is a symlink");
    return;
  }

  # create subdirectories if they do not exist
  mkpath($tpath,$debug,0755) unless -d $tpath;

  # save existing file, add suffix .pre_fcopy
  # what should I do if $destfile is a symlink?
  move($destfile,"$destfile.pre_fcopy") if -f $destfile;
  if (copy($sourcefile,$destfile)) {
    print "fcopy: copied $sourcefile to $destfile\n" ;
    postinst($ps,$destfile,$class);
    set_mode($ps,$destfile,$class);
  } else {
    ewarn("copy $sourcefile to $destfile failed. $!") ;
  }
}
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
sub postinst {

  my ($sourcefile,$destfile,$class) = @_;
  return unless -x "$sourcefile/postinst"; 
  warn "executing $sourcefile/postinst $class $destfile\n" if $debug;
  system "$sourcefile/postinst $class $destfile";
}
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
sub name2num {

  # convert names to numeric uid, gid
  my ($user,$group) = @_;
  my $uid = ($user  =~ /^\d+$/) ? $user  : getpwnam $user;
  my $gid = ($group =~ /^\d+$/) ? $group : getgrnam $group;
  warn "name2id $user = $uid ; $group = $gid\n" if $debug;
  return ($uid,$gid);
}
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
sub set_mode {

  # set target file's owner, group, mode and time
  # use owner,group,mode from -m or from the file file-modes or
  # use the values from the source file
  my ($sourcefile,$destfile,$class) = @_;
  my ($uid,$gid,$owner,$group,$mode);
  # get mtime,uid,gid,mode from source file
  my ($stime,@defmodes) = (stat("$sourcefile/$class"))[9,4,5,2];

  if ($modeset) { # use -m values
    ($owner,$group,$mode) = @opt_modes;
  } elsif (-f "$sourcefile/file-modes"){
    ($owner,$group,$mode) = read_file_mode("$sourcefile/file-modes",$class);
  } else { # use values from source file
    ($owner,$group,$mode) = @defmodes;
  }

  ($uid,$gid) = name2num($owner,$group);
  warn "chown/chmod u:$uid g:$gid m:$mode $destfile\n" if $debug; 
  chown ($uid,$gid,     $destfile) || ewarn("chown $owner $group $destfile failed. $!");
  chmod ($mode,         $destfile) || ewarn("chmod $mode $destfile failed. $!");
  utime ($stime,$stime, $destfile) || ewarn("utime for $destfile failed. $!");
}
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
sub check_mopt {

  # save and check -m options
  $modeset = 1;
  my $n = @opt_modes = split(/,/,$opt_m);
  ($n != 3) &&
    die "fcopy: wrong number of options for -m. Exact 3 comma separated items needed.\n";
   unless ($opt_modes[2] =~/^[0-7]+$/) {
     die "fcopy: file mode should be an octal number. Value is: $opt_modes[2]\n";
   }
  $opt_modes[2] = oct($opt_modes[2]);
}
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
sub read_file_mode {

  my ($modefile,$class) = @_;
  my ($owner,$group,$mode,$fclass,@defaults);

  warn "reading $modefile\n" if $verbose;
  open (MODEFILE,"<$modefile") || die "fcopy: can't open $modefile\n";
  while (<MODEFILE>) {
    # skip empty lines
    next if /^\s*$/;
    ($owner,$group,$mode,$fclass) = split;
    $mode = oct($mode);
    # class found
    return ($owner,$group,$mode) if ($fclass eq $class);
    # when no class is specified use data for all classes
    $fclass or @defaults = ($owner,$group,$mode);
  }
  close MODEFILE;
  return @defaults if @defaults;
  ewarn("no modes found for $class in $modefile");
}
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
sub read_classes {

  # read class names from a file
  my $file = shift;
  my @classes;

  open(CLASS,$file) || die "fcopy: can't open class file $file. $!\n";
  while (<CLASS>) {
    next if /^#/;
    push @classes, split;
  }
  close CLASS;
  return @classes;
}
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
sub ewarn {

  # print warnings and set error to 1
  $error = 1;
  warn "fcopy: @_\n";
}
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
sub read_status {
  my $n = my @status_files=split(/,/,$opt_P);
  ($n != 2) && die "fcopy: need both log and changes file\n";

  open(LASTLOG,$status_files[0]);
  while (<LASTLOG>) {
    s/\#.*//g;
    chomp;
    my ($source,$class) = split(/\s/,$_,2);
    $class=~s/\s*//g;
    $lastclass{$source} = $class;
  }
  close(LASTLOG);

  open(CHANGES,$status_files[1]);
  while (<CHANGES>) {
    s/\#.*//g;
    chomp;
    m#files/(\S+)$# and $changed{$1} = 1;
  }
  close(CHANGES);
}
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
sub read_files {

  # read list of files
  # lines starting with # are comments
  my $file = shift;
  my @list;

  open(LIST,"<$file") || die "fcopy: Can't open file $file\n";
  while (<LIST>) {
    next if /^#/;
    chomp;
    push @list, $_;
  }
  return @list;
}
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
sub usage {

  print << "EOF";
fcopy, copy files using classes. $version

   Copyright (C) 2001-2002 by Thomas Lange

Usage: fcopy [OPTION] ... SOURCE ...

   -c class[,class]     Define classes.
   -C file              Read classes from file.
   -d                   Remove target file if no class applies.
   -D                   Create debug output.
   -F file              Read list of sources from file.
   -h                   Show summary of options.
   -l                   Do not copy if destination is a symbolic link.
   -L file              Log destination and used class to file
   -m user,group,mode   Set user, group and mode for copied files.
   -P log,changes       Copy if class or source for class has changed since
                        previous run
   -r                   Copy recursivly but skip CVS directories.
   -s source_dir        Look for source files relative to source_dir.
   -t target_dir        Copy files relativ to target_dir.
   -v                   Create verbose output.

Report bugs to <lange\@informatik.uni-koeln.de>.
EOF
  exit 0;
}
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
sub rfilter {

  # Filter for recursive copying
  my $nlink;
  -d && ! ($_ =~ /^CVS$/) && ($nlink=(lstat($_))[3]) && 
    ( (-d 'CVS') && ($nlink==3) || ($nlink==2) ) && 
      push @rlist,$File::Find::name;
}
# - - - - - - - - - - - - - - - - - - - - - - - - - - - - - - -
# main program

$|=1;
$error = 0;
getopts('s:t:rm:vdDc:C:hF:lL:P:');
$opt_h && usage;
$opt_m && check_mopt();
$verbose = $opt_v || $ENV{verbose} || 0;
$debug   = $opt_D || $ENV{debug}   || 0;
$source  = $opt_s || "$ENV{FAI}/files" || `pwd`;
$target  = $opt_t || $ENV{FAI_ROOT} || $ENV{target};
$opt_L and (open(LOGFILE,">> $opt_L") || die("can't open logfile: $!"));
$opt_P and read_status();

#for postinst scripts
$ENV{'FAI_ROOT'}=$ENV{'target'}=$target;

# last class has highest priority
$ENV{classes} and @classes = reverse split /\s+/,$ENV{classes};
$opt_c and @classes = split /,/,$opt_c;
$opt_C and @classes = read_classes($opt_C);
warn join ' ','Classes:',@classes,"\n" if $debug;
$opt_F and @ARGV = read_files($opt_F);
die "fcopy: source undefined\n" unless $source;
die "fcopy: target undefined\n" unless $target;

if ($opt_r) {
  my $olddir=`pwd`;chdir "$source";
  File::Find::find(\&rfilter,@ARGV);
  @ARGV = @rlist;
  chdir "$olddir";
}

foreach (@ARGV) { copy_one($source,$_,$target); }
exit $error;
